module com.epistimis.face.GenerateFace

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*
import org.eclipse.xtext.xtext.generator.ui.*
import com.epistimis.xtextdocs.xtext.fragment.*
import com.epistimis.xtextdocs.xtext.formatter.*

var rootPath = ".."

Workflow {
	
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "com.epistimis.face"
				rootPath = rootPath
				runtimeTest = {
					enabled = true
				}
				eclipsePlugin = {
					enabled = true
				}
				eclipsePluginTest = {
					enabled = true
				}
				web = {
					enabled = true
				}
				createEclipseMetaData = true
			}
			code = {
				encoding = "UTF-8"
				lineDelimiter = "\n"
				fileHeader = 
"/*\n * generated by Xtext \${version}\n */
/*
 * Copyright (c) 2022 - 2024 Epistimis LLC (http://www.epistimis.com).
 */
"
				preferXtendStubs = false
			}
		}
		language = StandardLanguage {
			name = "com.epistimis.face.Face"
			fileExtensions = "face"
			referencedResource="platform:/resource/com.epistimis.uddl/model/generated/Uddl.genmodel"
			referencedResource="platform:/resource/com.epistimis.uddl.query/model/generated/Query.genmodel"

// Only enable this when we need to regenerate Langium
		    fragment = io.typefox.xtext2langium.Xtext2LangiumFragment {
		        outputPath = './langium'
		    }
// Only include this once we have a use for it.
//			fragment = org.eclipse.xtext.xtext.generator.ui.codemining.CodeMiningFragment {}
			serializer = {
				generateStub = false
			}
			validator = {
				composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
				// Generates checks for @Deprecated grammar annotations, an IssueProvider and a corresponding PropertyPage
				generateDeprecationValidation = true
				generateStub = true
			}
			formatter ={
				generateStub = true
				generateXtendStub = true
			}			
			generator = {
				generateXtendStub = true
				generateJavaMain = true
			}
			junitSupport = {
				junitVersion = "5"
			}
			fileWizard = {
				generate = true
			}
			projectWizard = {
				generate = true
			}			
			// https://idiomaticsoft.com/post/2022-05-16-xtext-importuri/ 
			scopeProvider = scoping.ImportNamespacesScopingFragment2 {
				generateXtendStub = false
			}
			
		 	// xtext-docs-gen
		 	fragment = DocsGeneratorFragment auto-inject {
		 		outputFileName = "face-syntax"
		 		formatter = HTMLDocsFormatter {
		 			includeToc = true
		 			includeSimplifiedGrammar = true
		 			mainTitle = "FACE Grammar Doc" // optional
		 			includeDotReferenceGraph = true
		 			
		 		}
		 	}

		}
	}
}
